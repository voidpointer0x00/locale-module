subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'voidpointer.locale'
    version = '1.0.0'

    java.sourceCompatibility = JavaVersion.VERSION_17
    java.targetCompatibility = JavaVersion.VERSION_17

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'UTF-8'
        }
    }

    test {
        useTestNG()
    }

    repositories {
        mavenCentral()
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        maven { url = "https://oss.sonatype.org/content/groups/public/" }
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    }

    dependencies {
        implementation 'org.jetbrains:annotations:24.0.1'
        annotationProcessor 'org.projectlombok:lombok:1.18.26'
        compileOnly 'org.projectlombok:lombok:1.18.26'
        testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:2.29.0'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = project.getGroup() + ':' + project.getName()
                    description = 'A locale module for Paper plugins using provided Adventure library'
                    url = 'https://github.com/voidpointer0x00/locale-module'

                    licenses {
                        license {
                            name = 'Do What The Fuck You Want To Public License, Version 2'
                            url = 'http://www.wtfpl.net/'
                        }
                    }
                    developers {
                        developer {
                            id = 'voidpointer'
                            name = 'Vasiliy'
                            email = 'voidpointer@ya.ru'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/voidpointer0x00/locale-module.git'
                        developerConnection = 'scm:git@github.com:voidpointer0x00/locale-module.git'
                        url = 'https://github.com/voidpointer0x00/locale-module'
                    }
                }
            }
        }
    }
}
